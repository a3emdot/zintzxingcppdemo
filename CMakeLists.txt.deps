cmake_minimum_required(VERSION 3.18)

project(ZintZXingCppDemoDeps)

include(ExternalProject)

set(ZINT_VERSION "2.12.0")
set(ZINT_MASTER_DIR https://github.com/zint/zint/archive)
set(ZINT_MASTER_FILE ${ZINT_MASTER_DIR}/${ZINT_VERSION}.tar.gz)
set(ZINT_SHA256 "83c76873cda163537de4b937d390de42a8ae487e830e73212c4a74c8cc49ce65")
set(ZINT_LOCAL_DIR third-party/zint)
set(ZINT_LOCAL_FILE ${CMAKE_CURRENT_SOURCE_DIR}/distfiles/zint-${ZINT_VERSION}.tar.gz)

if(NOT EXISTS ${ZINT_LOCAL_FILE})
  file(DOWNLOAD ${ZINT_MASTER_FILE} ${ZINT_LOCAL_FILE} SHOW_PROGRESS)
endif(NOT EXISTS ${ZINT_LOCAL_FILE})

add_custom_target(zint-fetch DEPENDS ${ZINT_LOCAL_FILE})

set(ZINT_PATCH0 fix-zint-exported-include-directories.diff)

ExternalProject_Add(
  zint-build
  DEPENDS zint-fetch
  SOURCE_DIR ${CMAKE_SOURCE_DIR}/${ZINT_LOCAL_DIR}
  BINARY_DIR ${CMAKE_BINARY_DIR}/${ZINT_LOCAL_DIR}
  STAMP_DIR ${CMAKE_BINARY_DIR}/${ZINT_LOCAL_DIR}/stamp
  URL ${ZINT_LOCAL_FILE}
  URL_HASH SHA256=${ZINT_SHA256}
  PATCH_COMMAND
    git apply -p1 ${CMAKE_CURRENT_SOURCE_DIR}/../../../${ZINT_PATCH0}
  LOG_PATCH ON
  CMAKE_ARGS -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_INSTALL_PREFIX=${CMAKE_BINARY_DIR}/prefix -DZINT_USE_QT:BOOL=OFF -DZINT_USE_PNG:BOOL=OFF
  TEST_COMMAND ""
)

set(ZXING_VERSION "2.0.0")
set(ZXING_MASTER_DIR https://github.com/nu-book/zxing-cpp/archive)
set(ZXING_MASTER_FILE ${ZXING_MASTER_DIR}/v${ZXING_VERSION}.tar.gz)
set(ZXING_SHA256 "12b76b7005c30d34265fc20356d340da179b0b4d43d2c1b35bcca86776069f76")
set(ZXING_LOCAL_DIR third-party/zxing-cpp)
set(ZXING_LOCAL_FILE ${CMAKE_CURRENT_SOURCE_DIR}/distfiles/zxing-cpp-v${ZXING_VERSION}.tar.gz)

if(NOT EXISTS ${ZXING_LOCAL_FILE})
  file(DOWNLOAD ${ZXING_MASTER_FILE} ${ZXING_LOCAL_FILE} SHOW_PROGRESS)
endif(NOT EXISTS ${ZXING_LOCAL_FILE})

add_custom_target(zxing-fetch DEPENDS ${ZXING_LOCAL_FILE})

set(ZXING_PATCH0 fix-zxing-cpp-array-out-of-bounds.diff)

ExternalProject_Add(
  zxing-build
  DEPENDS zxing-fetch
  SOURCE_DIR ${CMAKE_SOURCE_DIR}/${ZXING_LOCAL_DIR}
  BINARY_DIR ${CMAKE_BINARY_DIR}/${ZXING_LOCAL_DIR}
  STAMP_DIR ${CMAKE_BINARY_DIR}/${ZXING_LOCAL_DIR}/stamp
  URL ${ZXING_LOCAL_FILE}
  URL_HASH SHA256=${ZXING_SHA256}
  PATCH_COMMAND
    git apply -p1 ${CMAKE_CURRENT_SOURCE_DIR}/../../../${ZXING_PATCH0}
  LOG_PATCH ON
  CMAKE_ARGS -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE} -DCMAKE_INSTALL_PREFIX=${CMAKE_BINARY_DIR}/prefix -DBUILD_WRITERS:BOOL=OFF -DBUILD_EXAMPLES:BOOL=OFF -DBUILD_BLACKBOX_TESTS:BOOL=OFF -DBUILD_UNIT_TESTS:BOOL=OFF -DBUILD_PYTHON_MODULE:BOOL=OFF
  TEST_COMMAND ""
)

